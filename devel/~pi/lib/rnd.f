\ -----------------------------------------------------------------------------
\ __          ___       ____ ___
\ \ \        / (_)     |___ \__ \   Rnd for Windows
\  \ \  /\  / / _ _ __   __) | ) |  pi@alarmomsk.ru
\   \ \/  \/ / | | '_ \ |__ < / /   Библиотека для генерации случайных чисел
\    \  /\  /  | | | | |___) / /_   Pretorian 2007
\     \/  \/   |_|_| |_|____/____|  v 1.0
\ -----------------------------------------------------------------------------

MODULE: _RND

WINAPI: GetTickCount KERNEL32.DLL
0 VALUE RndNum \ Число для расчета случайного числа

EXPORT

0 VALUE RndEnd \ Верхняя граница случайного числа

\ Установить значение в счетчик для генерации псевдослучайных чисел
: Seed ( u -> )
	TO RndNum ;

\ Инициализировать генератор случайных чисел (инициализирован по умолчанию)
: RndInit ( -> )
	4294967295 TO RndEnd GetTickCount Seed ;

\ Получить 32 битное случайное число
: Random32 ( -> u )
	RndNum 0x8088405 * 1+ DUP TO RndNum ;

\ Получить случайное число
: Random ( -> u )
	RndEnd Random32 UM* NIP ;

\ Получить случайное число от 0 до указанного диапазона
: Choose ( u -> u )
	Random32 UM* NIP ;

;MODULE

RndInit

\EOF

---Свойства случайного числа---
RndEnd		( -> n ) - верхняя граница случайного числа

---Методы случайного числа---
RndInit		( -> ) - инициализировать генератор случайных чисел
Seed		( u -> ) - установить значение в счетчик для генерации
		псевдослучайных чисел
Random32	( -> u ) - получить 32 битное случайное число
Random		( -> u ) - получить случайное число
Choose		( u -> u ) - получить случайное число от 0 до указанного диапазона



