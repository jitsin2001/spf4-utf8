\ 20-05-2004 ~mOleg
\ Copyright [C] 2006-2007 mOleg mininoleg@yahoo.com
\ статические строковые преременные

 REQUIRE ?DEFINED devel\~moleg\lib\util\ifdef.f

\ создать строковую переменную, инициализируемую только при использовании
\ место для строки выделяется на HERE
\ возвращает адрес и размер буфера для строки
: String  ( # --> )
          CREATE 0 , ,
          ( --> addr # )
          DOES> DUP @     \ инициализирована?
                IF DUP @ SWAP CELL+ @
                 ELSE HERE TUCK OVER ! CELL+ @ DUP 1+ ALLOT ALIGN
                THEN ;


\ скопировать строку в Buf, добавить ноль в конце
: CopyZ ( Addr Buf # --> ) 2DUP + >R CMOVE 0 R> C! ;

\ копировать строку, представленную в виде Addr # в буфер длинной #buf
\ если длинна строки больше буфера, то копируется не более #buf символов
\ в памяти строка будет представлена в виде AscZ
: sCopy ( Addr # Addr #buf --> ) ROT MIN CopyZ ;

\ добавить к строке в памяти строку Asc #, результат объединения представить
\ в ASCIIZ виде. Если длинна строки превышает длинну буфера, то добавляемую
\ строку укоротить так, чтобы общий размер не превышал размера буфера.
: sPlus ( Asc # Addr #buf --> ) SWAP ASCIIZ> TUCK + >R - MIN R> SWAP CopyZ ;

\ получить строку из буфера, созданного по String
: sGet ( Addr #buf --> Addr # ) DROP ASCIIZ> ;

?DEFINED test{ \EOF -- тестовая секция ---------------------------------------

test{ \ пока просто тест на подключаемость.
  S" passed" TYPE
}test

\EOF
     Сначала  создается  имя  переменной, место под строку до первого вызова
имени  в системе не выделяется. После вызова возвращается адрес и размер уже
выделенного  буфера.  Дальше  в  такую  переменную  можно  записать строку с
помощью sCopy добавить к уже имеющейся строке новую с помощью sPlus а так же
получить результирующую строку с помощью sGet.
