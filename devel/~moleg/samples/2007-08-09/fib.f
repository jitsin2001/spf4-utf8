\ 08-09-2007 ~mOleg
\ об эффективности рекурсивных алгоритмов

\ Рекурсивный вариант нахождения n-того числа Фибоначчи
\ классический вариант, который идет как пример использования рекурсии
\ для нахождения n-того числа Фибоначчи
: FIBr ( n -- n' )
       DUP 1 >
       IF DUP 1 -
      RECURSE SWAP 2 -
      RECURSE +
       THEN ;

\ Циклический вариант нахождения n-того числа Фибоначчи
\ мой вариант.
: FIBl ( n --> fib )
       DUP IF 1 - ELSE EXIT THEN
       0 1 ROT
       BEGIN DUP WHILE
             >R OVER + SWAP
             R> 1 -
       REPEAT
       DROP + ;

\ вывод числа в поле длиной n символов
: .R ( n # --> )
     >R DUP >R ABS S>D <# #S R> SIGN #> R> OVER - 0 MAX SPACES TYPE ;

\ тестируем оба алгоритма
: testl TIMER@ 2>R FIBl TIMER@ ROT 15 .R SPACE 2R> D- D. SPACE ;
: testr TIMER@ 2>R FIBr TIMER@ ROT 15 .R SPACE 2R> D- D. SPACE ;

\ сравниваем два алгоритма по эффективности
: test 47 0 DO CR I 4 .R
                  I testl
                  I testr
            LOOP ;

\EOF    можно сколько угодно говорить о том, что сравнение данных алгоритмов не
 корректно, однако, это никак не отменяет того факта, что обычно для нахождения
 заданного числа Фибоначчи приводится именно рекурсивный алгоритм...  точно так
 же,  например,  обход  дерева  директорий  всегда  представляется  рекурсивным
 алгоритмом.

