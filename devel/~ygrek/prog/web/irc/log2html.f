\ $Id$
\ Конвертор IRC логов в HTML

REQUIRE STR@ ~ac/lib/str5.f
REQUIRE SPLIT ~pinka/samples/2005/lib/split.f
REQUIRE CONT ~profit/lib/bac4th.f
REQUIRE MAKE-IRC-MSG ~ygrek/lib/net/irc/basic.f
REQUIRE ENSURE ~ygrek/lib/debug/ensure.f
REQUIRE FileLines=> ~ygrek/lib/filelines.f
REQUIRE LAY-PATH ~pinka/samples/2005/lib/lay-path.f
REQUIRE TYPE>STR ~ygrek/lib/typestr.f
REQUIRE AsQName ~pinka/spf/quoted-word.f
REQUIRE OCCUPY ~pinka/samples/2005/lib/append-file.f
REQUIRE RAW-LOG-FILE ~ygrek/prog/web/irc/common.f
REQUIRE XHTML ~ygrek/lib/xhtml/core.f
REQUIRE logger ~ygrek/lib/log.f
REQUIRE >UTF8 ~ac/lib/lin/iconv/iconv.f

: curly S" {" ;

: put-header ( a u -- )
" <!DOCTYPE HTML PUBLIC {''}-//W3C//DTD HTML 4.0 Transitional//EN{''}>
<HTML>
<HEAD>
<META HTTP-EQUIV={''}Content-Type{''} CONTENT={''}text/html; charset=utf-8{''}>
<META NAME={''}title{''} CONTENT={''}Log of #forth{''}>
<META NAME={''}Author{''} CONTENT={''}exsample{''}>
<META NAME={''}Keywords{''} CONTENT={''}logs, logging, channel, irc, bot, exsample, log2html{''}>
<META NAME={''}robots{''} CONTENT= {''}index,all{''}>
<style type={''}text/css{''}>
<!--
.time {curly}color: #AAAAAA; text-decoration: none;}
.else {curly}color: #009900; font-style: italic;}
.nick {curly}color: #0000AA;}
.self {curly}color: #AA0099;}
//-->
</style>
<TITLE>Log of #forth. {s}</TITLE>
</head>
<body>"
STYPE ;

: put-footer
" <hr/>
<span style={''}color:black; font-size: 0.8em;{''}>Generated by log2html for exsample</span>
</body>
</html>"
STYPE ;

0 VALUE time
0 VALUE msg

ALSO XHTML
ALSO XMLSAFE

: font PRO %[ `class $$ ]% `font atag CONT ;

: put-time ( a u -- ) 
  time STR@ 2DUP 2DUP " <a id={''}{s}{''} href={''}#{s}{''} class={''}time{''}>[{s}]</a>" FORTH::STYPE ;
: put-nick ( a u -- ) `nick font `< TYPE TYPE `> TYPE ;
: put-text ( a u -- ) SPACE TYPE ;
: sput-text SPACE STYPE ;
: sput-self `self font STYPE ;
: sput-else ( s -- ) `else font STYPE ;
: put-break ( -- ) `br /tag ;

PREVIOUS
PREVIOUS

: line=> PRO put-time CONT put-break ;

: put-message-text
   msg irc-msg-text irc-action?
   IF msg irc-msg-sender " {s} {s}" sput-self
   ELSE msg irc-msg-sender put-nick ( a u ) put-text
   THEN ;

MODULE: BOT-LOG-TALK

: PRIVMSG line=> put-message-text ;
: NICK line=> msg irc-msg-text msg irc-msg-sender " {s} changed nick to {s}" sput-else ;
: QUIT line=> msg irc-msg-text msg irc-msg-sender " {s} quit ({s})" sput-else ;
: PART line=> msg irc-msg-text msg irc-msg-target msg irc-msg-sender " {s} left {s} ({s})" sput-else ;
: JOIN line=> msg irc-msg-text msg irc-msg-sender " {s} joined {s}" sput-else ;

: NOTFOUND -1 THROW ;

: '' '' ;

;MODULE

: each-line ( a u -- )
   S" |" SPLIT- ENSURE >STR TO time
   >UTF8 OVER { addr }
   MAKE-IRC-MSG 0= IF FREE-IRC-MSG EXIT THEN TO msg
   GET-ORDER
   ONLY BOT-LOG-TALK
   msg irc-msg-cmd ['] EVALUATE CATCH IF 2DROP THEN
   SET-ORDER
   msg FREE-IRC-MSG
   time STRFREE
   addr FREE THROW
;

: (log2html) { d m y -- }
   d m y RAW-LOG-FILE FILE-EXISTS NOT IF EXIT THEN
   " {#d}/{#m}/{#y}" DUP STR@ put-header STRFREE
   d m y RAW-LOG-FILE
   START{ FileLines=> DUP STR@ each-line }EMERGE
   put-footer ;

: log2html { d m y | s -- }
   d m y ['] (log2html) TYPE>STR-CATCH IF S" log2html failed" log::error STRFREE DROP DROP DROP EXIT THEN
   -> s
   s STRLEN 0 <> IF
     s STR@
     d m y HTML-LOG-FILE
     FORCE-PATH OCCUPY
   THEN
   s STRFREE ;

