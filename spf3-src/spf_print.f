( Слова форматной печати чисел.
  Copyright [C] 1992-1999 A.Cherezov ac@forth.org
  Преобразование из 16-разрядного в 32-разрядный код - 1995-96гг
  Ревизия - сентябрь 1999 [переход на USER-переменные и
  замена CODE-слов высокоуровневыми определениями]
)

USER     HLD  \ переменная - позиция последней литеры, перенесенной в PAD

USER     BASE ( -- a-addr ) \ 94
\ a-addr - адрес ячейки, содержащей текущее основание системы счисления (2..36).

4096 TC-USER-ALLOT
USER-CREATE PAD ( -- c-addr ) \ 94 CORE EXT
\ c-addr - адрес области для промежуточной обработки данных.
1024 TC-USER-ALLOT

: HEX ( -- ) \ 94 CORE EXT
\ Установить содержимое BASE равным шестнадцати.
  16 BASE !
;
: DECIMAL ( -- ) \ 94
\ Установить основание системы счисления равным десяти.
  10 BASE !
;

: HOLD ( char -- ) \ 94
\ Добавить char к началу форматной числовой строки.
\ Исключительная ситуация возникает, если использовать HOLD
\ вне <# и #>, ограничивающивающих преобразование чисел.
  HLD @ 1- DUP HLD ! C!
;

: <# ( -- ) \ 94
\ Начать форматное преобразование чисел.
  PAD HLD !
;

: # ( ud1 -- ud2 ) \ 94
\ Делением ud1 на значение BASE выделить одну цифру с конца и
\ добавить ее в буфер форматного преобразования чисел,
\ оставив частное ud2.
\ Исключительная ситуация возникает, если использовать #
\ вне <# и #>, ограничивающивающих преобразование чисел.
  0 BASE @ UM/MOD >R BASE @ UM/MOD R>
  ROT DUP 10 < IF 48 + ELSE 55 + THEN
  HOLD
;

: #S ( ud1 -- ud2 ) \ 94
\ Выделять цифры D1 по слову # до получения нуля.
\ ud2 - ноль.
\ Исключительная ситуация возникает, если использовать #S
\ вне <# и #>, ограничивающивающих преобразование чисел.
  BEGIN
    # 2DUP D0=
  UNTIL
;

: #> ( xd -- c-addr u ) \ 94
\ Убрать xd. Сделать буфер форматного преобразования доступным в виде
\ строки символов, заданной c-addr и u.
\ Программа может менять символы в этой строке.
  2DROP HLD @ PAD OVER -
;

: SIGN ( n -- ) \ 94
\ Если n отрицательно, добавить в строку форматного преобразования
\ чисел минус.
\ Исключительная ситуация возникает, если использовать SIGN
\ вне <# и #>, ограничивающивающих преобразование чисел.
  0< IF [CHAR] - HOLD THEN
;

: D. ( d -- ) \ 94 DOUBLE
\ Вывести d на дисплей в свободном формате.
  DUP >R DABS <# #S R> SIGN #>
  TYPE SPACE
;

: . ( n -- ) \ 94
\ Напечатать n в свободном формате.
  S>D D.
;

: U. ( u -- ) \ 94
\ Напечатать u в свободном формате.
  U>D D.
;

: .0
  >R 0 <# #S #> R> OVER - 0 MAX DUP
    IF 0 DO [CHAR] 0 EMIT LOOP
    ELSE DROP THEN TYPE
;

: >PRT
  DUP BL U< IF DROP [CHAR] . THEN
;

: PTYPE
  0 DO DUP C@ >PRT EMIT 1+ LOOP DROP
;

: DUMP ( addr u -- ) \ 94 TOOLS
  DUP 0= IF 2DROP EXIT THEN
  15 + 16 U/ 0 DO
    CR BASE @ SWAP 16 BASE ! DUP 4 .0 SPACE
    SPACE DUP 16 0
      DO I 4 MOD 0= IF SPACE THEN
        DUP C@ 2 .0 SPACE 1+
      LOOP SWAP 16  PTYPE
  SWAP BASE ! LOOP DROP
;

: (.") ( T -> )
  COUNT TYPE
;
' (.") TO (.")-CODE

: >NUMBER ( ud1 c-addr1 u1 -- ud2 c-addr2 u2 ) \ 94
\ ud2 - результат преобразования символов строки, заданной c-addr1 u1,
\ в цифры, используя число в BASE, и добавлением каждой к ud1 после
\ умножения ud1 на число в BASE. Преобразование продолжается слева
\ направо до первого непреобразуемого символа, включая символы "+" и "-",
\ или до полного преобразования строки.
\ c-addr2 - адрес первого непреобразумого символа или первого символа
\ за концом строки, если строка была полностью преобразована.
\ u2 - число непреобразованных символов в строке.
\ Неоднозначная ситуация возникает, если ud2 переполняется во время
\ преобразования.
  BEGIN
    DUP
  WHILE
    >R
    DUP >R
    C@ BASE @ DIGIT 0=     \ ud n flag
    IF R> R> EXIT THEN     \ ud n  ( ud = udh udl )
    SWAP BASE @ UM* DROP   \ udl n udh*base
    ROT BASE @ UM* D+      \ (n udh*base)+(udl*baseD)
    R> 1+ R> 1-
  REPEAT
;
: ANSI>OEM ( addr u -- addr u )
  DUP ROT ( u u addr )
  PAD SWAP CharToOemBuffA DROP
  PAD SWAP
;
: OEM>ANSI ( addr u -- addr u )
  DUP ROT ( u u addr )
  PAD SWAP OemToCharBuffA DROP
  PAD SWAP
;
