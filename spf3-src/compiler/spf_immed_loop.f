( Слова немедленного выполнения, используемые при компиляции
  циклов в теле высокоуровневого определения.
  ОС-независимые определения.
  Copyright [C] 1992-1999 A.Cherezov ac@forth.org
  Преобразование из 16-разрядного в 32-разрядный код - 1995-96гг
  Ревизия - сентябрь 1999
)

HEX

CODE J   \ 94
\ Интерпретация: семантика неопределена.
\ Выполнение: ( -- n|u ) ( R: loop-sys -- loop-sys )
\ n|u - копия параметра следующего объемлющего цикла.
\ Неоднозначная ситуация возникает, если параметр недоступен.
     XCHG EBP, ESP
     MOV EAX, 10 [EBP]
     SUB EAX, 14 [EBP]
     PUSH EAX
     XCHG EBP, ESP
     RET
END-CODE

( inline'ы для компиляции циклов )

CODE C-DO
      XCHG EBP, ESP
      POP  ECX
      POP  EAX
      XCHG EBP, ESP
      MOV  EBX, # 80000000
      SUB  EBX, EAX
      ADD  ECX, EBX
      MOV  EAX, # 0
      PUSH EAX  \ LEAVE
      PUSH EBX  \ 80000000h-to
      PUSH ECX  \ 80000000H-to+from
END-CODE

CODE C-?DO
      XCHG EBP, ESP
      POP  ECX
      POP  EDX 
      XCHG EBP, ESP
      MOV  EAX, # 0
      CMP  EDX, ECX
      JNZ  @@1
      JMP  EAX
@@1:  MOV  EBX, # 80000000
      SUB  EBX, EDX
      ADD  ECX, EBX
      PUSH EAX
      PUSH EBX
      PUSH ECX
END-CODE

CODE C-LOOP
      POP  EAX
      INC  EAX
      PUSH EAX
      JO   @@1
      JMP  0
@@1:  ADD  ESP, # 0C
END-CODE

CODE C-+LOOP
      POP  EAX
      ADD  EAX, [EBP]
      PUSH EAX
      JO   @@1
      ADD  EBP, # 4
      JMP  0
@@1:  ADD  EBP, # 4
      ADD  ESP, # 0C
END-CODE

CODE C-I
      XCHG EBP, ESP 
      MOV  EAX, [EBP]
      SUB  EAX, 4 [EBP]
      PUSH EAX
      XCHG EBP, ESP
END-CODE

CODE C-LEAVE
      ADD ESP, # 8
      RET
END-CODE

DECIMAL

: DO   \ 94
\ Интерпретация: семантика неопределена.
\ Компиляция: ( C: -- do-sys )
\ Положить do-sys на стек управления. Добавить семантику времени выполнения, 
\ данную ниже, к текущему определению. Семантика незавершена до разрешения
\ потребителем do-sys, таким как LOOP.
\ Время выполнения: ( n1|u1 n2|u2 -- ) ( R: -- loop-sys )
\ Установить параметры цикла на индекс n2|u2 и предел n1|u1. Неопределенная 
\ ситуация возникает, если n1|u1 и n2|u2 не одного типа. Все, что уже 
\ находилось на стеке возвратов, становится недоступным до тех пор, пока не 
\ будут убраны параметры цикла.
  ?COMP ['] C-DO
  HERE 25 DUP ALLOT MOVE
  HERE 7 - HERE
; IMMEDIATE

: ?DO   \ 94 CORE EXT
\ Интерпретация: семантика неопределена.
\ Компиляция: ( C: -- do-sys )
\ Положить do-sys на стек управления. Добавить семантику времени выполнения, 
\ данную ниже, к текущему определению. Семантика незавершена до разрешения
\ потребителем do-sys, таким как LOOP.
\ Время выполнения: ( n1|u1 n2|u2 -- ) ( R: --  | loop-sys )
\ Если n1|u1 равно n2|u2, продолжить выполнение с места, данного потребителем 
\ do-sys. Иначе установить параметры цикла на индекс n2|u2 и предел n1|u1
\ и продолжить выполнение сразу за ?DO. Неопределенная 
\ ситуация возникает, если n1|u1 и n2|u2 не одного типа. Все, что уже 
\ находилось на стеке возвратов, становится недоступным до тех пор, пока не 
\ будут убраны параметры цикла.

  ?COMP ['] C-?DO
  HERE 35 DUP ALLOT MOVE
  HERE 27 - HERE
; IMMEDIATE

: LOOP   \ 94
\ Интерпретация: ( C: do-sys -- )
\ Добавить семантику времени выполнения, данную ниже, к текущему определению.
\ Разрешить все появления LEAVE между позицией, данной do-sys и следующей
\ позицией передачи управления для выполнения слов за LOOP.
\ Время выполнения: ( -- ) ( R: loop-sys1 --  | loop-sys2 )
\ Неопределенная ситуация возникает, если параметры цикла недоступны.
\ Прибавить единицу к индексу цикла. Если индекс цикла стал равным пределу, 
\ убрать параметры цикла и продолжить выполнение сразу за циклом. Иначе 
\ продолжить выполнение с начала цикла.
  ?COMP ['] C-LOOP
  HERE 18 DUP ALLOT MOVE
  HERE 3 - - HERE 7 - !
  HERE SWAP !
; IMMEDIATE

: +LOOP    \ 94
\ Интерпретация: ( C: do-sys -- )
\ Добавить семантику времени выполнения, данную ниже, к текущему определению.
\ Разрешить все появления LEAVE между позицией, данной do-sys и следующей
\ позицией передачи управления для выполнения слов за LOOP.
\ Время выполнения: ( n -- ) ( R: loop-sys1 --  | loop-sys2 )
\ Неопределенная ситуация возникает, если параметры цикла недоступны.
\ Прибавить n к индексу цикла. Если индекс цикла не пересек границу между
\ пределом цикла минус единица и пределом цикла, продолжить выполнение с
\ начала цикла. Иначе убрать параметры цикла и продолжить выполнение сразу
\ за циклом.
  ?COMP ['] C-+LOOP
  HERE 25 DUP ALLOT MOVE
  HERE 6 - - HERE 10 - !
  HERE SWAP !
; IMMEDIATE

: I   \ 94
\ Интерпретация: семантика неопределена.
\ Выполнение: ( -- n|u ) ( R: loop-sys -- loop-sys )
\ n|u - копия текущего (внутреннего) индекса цикла. Неопределенная ситуация 
\ возникает, если парметры цикла недоступны.
  ?COMP ['] C-I
  HERE 11 DUP ALLOT MOVE
; IMMEDIATE

: LEAVE    \ 94
\ Интерпретация: семантика неопределена.
\ Выполнение: ( -- ) ( R: loop-sys -- )
\ Убрать текущие параметры цикла. Неопределенная ситуация возникает, если 
\ они недоступны. Продолжить выполнение сразу за самыми внутренними DO ... LOOP 
\ или DO ... +LOOP.
  ?COMP ['] C-LEAVE
  HERE 7 DUP ALLOT MOVE
; IMMEDIATE

: UNLOOP  \ 94
\ Интерпретация: семантика неопределена.
\ Выполнение: ( -- ) ( R: loop-sys -- )
\ Убрать параметры цикла текущего уровня. UNLOOP требуется для каждого
\ уровня вложения циклов перед выходом из определения по EXIT.
\ Неоднозначная ситуация возникает, если параметры цикла недоступны.
  3 0 DO ['] RDROP COMPILE, LOOP
; IMMEDIATE
